WHITESPACE = _{ " " }

description = { lb? ~ colors ~ generators ~ derived ~ presets ~ EOI }

lb = _{ "\n"+ }
ident = @{ ( !(WHITESPACE | "\n" | "~" | "/") ~ ANY )+ }
number = @{ ASCII_DIGIT+ }

colors = { "COLORS" ~ lb ~ color+ }
color = { ident ~ "->" ~ number ~ ("," ~ number)* ~ lb }

generators = { "GENERATORS" ~ lb ~ generator+ }

generator = { ident ~ "=" ~ cycle+ ~ lb }
cycle = { "(" ~ number ~ ("," ~ number)* ~ ")" }

derived = { "DERIVED" ~ lb ~ derivation* }
derivation = { ident ~ "=" ~ ident+ ~ lb }

presets = { "PRESETS" ~ lb ~ preset* }
preset = { orders ~ algos ~ ("~" ~ number)? ~ lb }
orders = { "(" ~ number ~ ("," ~ number)* ~ ")" }
algos = { algo ~ ("/" ~ algo)* }
algo = { (!"/" ~ ident)+ }

